plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.example.examplelib'
    compileSdk 33


    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                // Replace with your own group ID.
                groupId 'com.example'

                // Replace with the name of your library
                artifactId 'example-lib'
                version '1.0.0'
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"

                // Replace GITHUB_USERID with your personal or organisation user ID and
                // REPOSITORY with the name of the repository on GitHub
                url = uri("https://maven.pkg.github.com/szaboa/android-lib-github-packages")

                credentials {
                    username = githubProperties['gpr.usr']
                    password = githubProperties['gpr.key']
                }
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}